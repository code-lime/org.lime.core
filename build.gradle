plugins {
    id 'java-library'
    id 'maven-publish'
    id "fr.brouillard.oss.gradle.jgitver" version "+"
}

static Map<String, ?> parseAttributes(byte[] bytes) {
    try (def input = new ByteArrayInputStream(bytes)) {
        def jdkManifest = new java.util.jar.Manifest(input)
        return jdkManifest.mainAttributes.entrySet().collectEntries { Map.Entry<java.util.jar.Attributes.Name, Object> e ->
            Map.entry(e.key.toString(), e.value)
        }
    }
}
static Iterable<org.gradle.jvm.tasks.Jar> jarTasks(Project target, String name) {
    def remapName = "remap${name.capitalize()}"
    return target.tasks
            .withType(org.gradle.jvm.tasks.Jar)
            .named { jit -> jit == name || jit == remapName }
}

TaskProvider<Jar> registerResultJarTask(String prefix) {
    def taskName = prefix.isEmpty() ? "jar" : "${prefix}Jar"
    def resultTaskName = "result${taskName.capitalize()}"
    return tasks.register(resultTaskName, Jar) {
        archiveClassifier.set(prefix)
        destinationDirectory.set(layout.buildDirectory.dir("libs"))
        dependsOn subprojects.collect { jarTasks(it, taskName) }

        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        from {
            subprojects.collect {
                jarTasks(it, taskName).collect {
                    zipTree(it.archiveFile)
                }
            }
        }

        manifest {
            from {
                subprojects.forEach { proj ->
                    jarTasks(proj, taskName).forEach { v ->
                        zipTree(v.archiveFile)
                                .matching { include 'META-INF/MANIFEST.MF' }
                                .forEach { manifest.attributes(parseAttributes(it.readBytes())) }
                    }
                }
                project.resources.text.fromString("")
            }
        }
    }
}

def resultJarTask = registerResultJarTask("")
def resultSourcesJarTask = registerResultJarTask("sources")

allprojects {
    repositories {
        mavenCentral()
    }
}

apply from: 'global.gradle'

//<editor-fold desc="Settings: Maven">
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(resultJarTask.get()) {
                classifier = null
            }
            artifact(resultSourcesJarTask.get()) {
                classifier = "sources"
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
            credentials {
                username = System.getenv("GITHUB_OWNER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
//</editor-fold>

//<editor-fold desc="Settings: Version">
version = "$version"
boolean hasPostfixOriginal = false
if (version.contains('-')) {
    hasPostfixOriginal = true
    String[] argsVersion = version.split('-', 2)
    if (argsVersion[1] == "0") {
        version = argsVersion[0]
    } else {
        version = "${argsVersion[0]}-alpha.${argsVersion[1]}"
    }
}
var globalProps = getProperties()
boolean isTagCommit = "LIGHTWEIGHT" == globalProps["base_tag_type"]
if (!hasPostfixOriginal && !isTagCommit) {
    var argsVersion = version.split('\\.')
    int lastIndex = argsVersion.length - 1
    String lastValue = argsVersion[lastIndex]
    try {
        lastValue = Integer.toString(Integer.parseUnsignedInt(lastValue) + 1) + "-alpha.0"
    } catch (ignored) {
        lastValue = lastValue + "-unsafe"
    }
    argsVersion[lastIndex] = lastValue
    version = String.join(".", argsVersion)
}

def versionShare = version
allprojects {
    version = "${versionShare}"
}
//</editor-fold>

build {
    dependsOn(resultJarTask, resultSourcesJarTask)
}
plugins {
    id 'java'
    id 'maven-publish'
}

overrideJavaVersion = 17

apply from: '../global.gradle'

dependencies {
    implementation project(':common')
}

versionByteBuddy = project(":common").versionByteBuddy
versionCommonsIO = project(":common").versionCommonsIO
versionCommonsLang3 = project(":common").versionCommonsLang3
versionGuice = project(":common").versionGuice

projectCombineJars(project(':common'))

//<editor-fold desc="Settings: Plugin">
ext {
    pluginName = "${group}.core"
    pluginId = "${group}.${pathName}".toLowerCase().replaceAll("[.:/]", "-")
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly "com.velocitypowered:velocity-api:${versionVelocity}"
    annotationProcessor "com.velocitypowered:velocity-api:${versionVelocity}"
}

processResources {
    configureProcessResources(delegate, "velocity-dependency.json", [
            group: "${project.group}",
    ])
}
configureTemplates([
        pluginName: "${group}.core",
        pluginId: "${group}.${pathName}".toLowerCase().replaceAll("[.:/]", "-")
])
//</editor-fold>

configurations {
    embedded
}

//<editor-fold desc="Settings: Libraries">
//<editor-fold desc="Settings: Libraries: ByteBuddy">
dependencies {
    compileOnly "net.bytebuddy:byte-buddy-agent:${versionByteBuddy}"
    compileOnly "net.bytebuddy:byte-buddy:${versionByteBuddy}"

    embedded "net.bytebuddy:byte-buddy-agent:${versionByteBuddy}"
    embedded "net.bytebuddy:byte-buddy:${versionByteBuddy}"
}
//</editor-fold>
//<editor-fold desc="Settings: Libraries: Libby">
repositories {
    maven {
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compileOnly "com.alessiodp.libby:libby-velocity:${versionLibby}"
    embedded "com.alessiodp.libby:libby-velocity:${versionLibby}"
}
//</editor-fold>
//<editor-fold desc="Settings: Libraries: ObjectWebAsm">
dependencies {
    compileOnly "org.ow2.asm:asm:${versionObjectWebAsm}"
}
//</editor-fold>
//<editor-fold desc="Settings: Libraries: Apache Commons">
dependencies {
    compileOnly "commons-io:commons-io:${versionCommonsIO}"
    compileOnly "org.apache.commons:commons-lang3:${versionCommonsLang3}"
}
//</editor-fold>
//<editor-fold desc="Settings: Libraries: com.google.inject#guice">
dependencies {
    compileOnly "com.google.inject:guice:${versionGuice}"
}
//</editor-fold>
//</editor-fold>

jar {
    into("velocity-libs") {
        from {
            configurations.embedded.filter { it.name.endsWith(".jar") }
        }
    }
}

//<editor-fold desc="Settings: Maven">
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = "${pathName}"
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
            credentials {
                username = System.getenv("GITHUB_OWNER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
//</editor-fold>

println("VELOCITY: ${group}/${pathName}#${version}")

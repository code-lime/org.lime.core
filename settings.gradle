pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url = 'https://maven.fabricmc.net/' }
        maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    }
}

dependencyResolutionManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url = 'https://maven.fabricmc.net/' }
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    }
}

rootProject.name = 'core'

include('common')

String[] existIncludes = ['fabric', 'paper', 'velocity']

String[] includeList = null
if (hasProperty('includeList')) {
    includeList = "${property('includeList')}".split(',')
    if (includeList.length == 1 && includeList[0] == '') {
        includeList = []
    }
}

if (includeList != null)
    existIncludes = existIncludes.findAll { includeList.contains(it) }

existIncludes.each { include(it) }
